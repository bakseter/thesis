\chapter{Implementation of Case-Specific Notions}
\label{chap:implementation}

\section{Data types}

As discussed in \Cref{ch:the_case_in_question}, we want to represent clauses as
a set of atoms as premises and a single atom as a conclusion.
We implement this in Coq using two types, \lstinline{Atom} and \lstinline{Clause}.

\begin{minipage}{\linewidth}
\begin{lstlisting}[language=Coq, label={lst:atom_clause_def}, caption={\lstinline{Atom} and \lstinline{Clause} in Coq}]
Inductive Atom : Type :=
  | atom : string -> nat -> Atom.

Notation "x & k" := (atom x k) (at level 80).

Inductive Clause : Type :=
  | clause : set Atom -> Atom -> Clause.

Notation "ps ~> c" := (clause ps c) (at level 81).
\end{lstlisting}
\end{minipage}

Note also the \lstinline{Notation}-syntax, which allow us to define a custom notation,
making the code easier to read. The expression on the left-hand side of the \lstinline{:=} in quotation marks
is definitionally equal to the expression on the right-hand side in parentheses.
The level determines which notation should take precedence, with a higher level equaling a higher precedence.

We also want to represent functions of the form $f : V \rightarrow \mathbb{N}^{\infty}$,
which as we saw in \Cref{sec:relevant_parts_of_the_paper}
are functions that are models of a set of clauses.
We implement this in Coq using two types, \lstinline{Ninfty} and \lstinline{Frontier}.
\lstinline{Ninfty} has two constructors; \lstinline{infty} represents $\infty$,
and \lstinline{fin n} represents a natural number $n$.
\lstinline{Frontier} is a function from strings (variables) to \lstinline{Ninfty}.
In the context of our formalization, we will refer to a function of this
type as a \emph{frontier}.

\begin{minipage}{\linewidth}
\begin{lstlisting}[language=Coq, label={lst:ninfty_frontier_def}, caption={\lstinline{Ninfty} and \lstinline{Frontier} in Coq}]
Inductive Ninfty : Type :=
  | infty : Ninfty
  | fin   : nat -> Ninfty.

Definition Frontier := string -> Ninfty.
\end{lstlisting}
\end{minipage}

Using these definitions of \lstinline{Atom}, \lstinline{Clause} and \lstinline{Frontier},
we can define functions that check whether any given atom or clause is satisfied a given frontier.

\begin{minipage}{\linewidth}
\begin{lstlisting}[language=Coq, label={lst:atom_clause_true_def}, caption={\lstinline{atom_true} and \lstinline{clause_true} in Coq}]
Definition atom_true (a : Atom) (f : Frontier) : bool :=
  match a with
  | (x & k) =>
    match f x with
    | infty => true
    (* see explanation for <=? below *)
    | fin n => k <=? n
    end
  end.

Definition clause_true (c : Clause) (f : Frontier) : bool :=
  match c with
  | (conds ~> conc) =>
    (* every atom in conds is true *)
    if fold_right andb true (map (fun a => atom_true a f) conds)
    then (atom_true conc f)
    else true
  end.
\end{lstlisting}
\end{minipage}

The infix function \lstinline{<=?} is the boolean (and hence decidable) version of the
Coq function \lstinline{<=}, which uses \lstinline{Prop} and is not inherently decidable without
additional lemmas.

We can also define functions that "shift" the number value of atoms or whole clauses by some amount \lstinline{n : nat}.

\begin{minipage}{\linewidth}
\begin{lstlisting}[language=Coq, label={lst:shift_atom_clause_true}, caption={\lstinline{shift_atom} and \lstinline{shift_clause} in Coq}]
Definition shift_atom (n : nat) (a : Atom)  : Atom :=
  match a with
  | (x & k) => (x & (n + k))
end.

Definition shift_clause (n : nat) (c : Clause) : Clause :=
  match c with
  | conds ~> conc =>
    (map (shift_atom n) conds) ~> (shift_atom n conc)
  end.
\end{lstlisting}
\end{minipage}

Using these definitions, we can now define an important property that is
possible by \Cref{lem:3.1} \cite[p.~3]{mbezem}, since this lemma enables us to check whether or not a clause
is satisfied by a frontier for any shift of \lstinline{k : nat}.
We will use this property later to determine if a frontier is a valid model for a set of clauses.

\begin{minipage}{\linewidth}
\begin{lstlisting}[language=Coq, label={lst:all_shifts_true}, caption={\lstinline{all_shifts_true} in Coq}]
Definition all_shifts_true (c : Clause) (f : Frontier) : bool :=
  match c with
  | (conds ~> conc) =>
      match conc with
      | (x & k) =>
          match f x with
          | infty => true
          | fin n => clause_true (shift_clause (n + 1 - k) c) f
          end
      end
  end.
\end{lstlisting}
\end{minipage}

The idea is to shift the clause up by the smallest shift that makes the conclusion false
and then to check that this shifted clause is still true
(because one of the atoms in the condition also has become false).

\section{Semantic functions and predicates}

\subsection{The function \lstinline{sub_model}}

Given any set of clauses and a frontier (function assigning values to the variables),
we can determine if the frontier is a model of the set of clauses, i.e.
whether all shifts of all clauses are satisfied by the frontier.

We translate this property to Coq as the recursive function \lstinline{sub_model}.
As we saw in \Cref{sec:relevant_parts_of_the_paper}, we distinguish
between three different sets of clauses when talking about frontiers as models;
$\overline{S_C}$, $\overline{S_C}\mid W$ and $\overline{S_C}\downarrow W$.
These are represented by
\lstinline{sub_model Cs V V f}, \lstinline{sub_model Cs W W f} and \lstinline{sub_model Cs V W f},
respectively, where \lstinline{V} is the set of all variables.

We have two additional arguments \lstinline{V} and \lstinline{W}:
\lstinline{V} is the the set of variables (strings) from the set of clauses \lstinline{Cs},
and \lstinline{W} is a subset of \lstinline{V} such that for each variable
there is a clause in \lstinline{Cs} that can be used to generate a new atom.
The function \lstinline{vars_set_atom} used below simply returns all the variables
used in a set of atoms, as a set of strings.

\begin{minipage}{\linewidth}
\begin{lstlisting}[language=Coq, label={lst:sub_model_def}, caption={The function \lstinline{sub_model} in Coq}]
Fixpoint sub_model (Cs : set Clause) (V W : set string) (f : Frontier) : bool :=
  match Cs with
  | []      => true
  | (l ~> (x & k)) :: t  =>
    (* conclusion not in W *)
    (negb (set_mem string_dec x W) ||
    (* some premise not in V *)
     negb (
       fold_right andb true
         (map (fun x => set_mem string_dec x V) (vars_set_atom l))
     ) ||
     all_shifts_true (l ~> (x & k)) f
    ) && sub_model t V W f
  end.
\end{lstlisting}
\end{minipage}

\subsection{The functions \lstinline{sub_vars_improvable} and \lstinline{sub_forward}}

The three conditions of the disjunction in \Cref{lst:sub_model_def} are
what determines if a clause is satisfied by a frontier.
If these conditions are not met, we want to repeatedly increment the value of the variable,
and check again if the clause is satisfied.
We start by first determining such "improvable" variables with the function \lstinline{sub_vars_improvable}:

\begin{minipage}{\linewidth}
\begin{lstlisting}[language=Coq, label={lst:sub_vars_improvable_def}, caption={The function \lstinline{sub_vars_improvable}}]
Fixpoint sub_vars_improvable (Cs : set Clause) (V W : set string) (f : Frontier)
    : set string :=
  match Cs with
  | []      => []
  | (l ~> (x & k)) :: t  =>
      if negb (set_mem string_dec x W) ||
         negb (
            fold_right andb true
                (map (fun x => set_mem string_dec x V) (vars_set_atom l))
         ) ||
         all_shifts_true (l ~> (x & k)) f
      then sub_vars_improvable t V W f
      else set_add string_dec x (sub_vars_improvable t V W f)
  end.
\end{lstlisting}
\end{minipage}

We then define a new function \lstinline{sub_forward} that takes these improvable variables
and a frontier \lstinline{f}, and defines a new frontier \lstinline{f'}
that increments the value that \lstinline{f} assigns to each variable.
The return type of \lstinline{sub_forward} is a pair of a set of strings and a frontier,
where the set of strings is the set of improvable variables.

\begin{minipage}{\linewidth}
\begin{lstlisting}[language=Coq, label={lst:sub_forward_def}, caption={The function \lstinline{sub_forward}}]
Definition sub_forward (Cs : set Clause) (V W : set string) (f : Frontier)
    : set string * Frontier :=
  let X := sub_vars_improvable Cs V W f in
  (* Sinfty increases a Fin value by 1, and leaves Infty unchanged *)
  let f' := fun v => if set_mem string_dec v X then Sinfty (f v) else f v
  in (X, f').
\end{lstlisting}
\end{minipage}

\subsection{The function \lstinline{geq}}

We want to determine whether all the values assigned to a set of variables
from one frontier are greater than or equal to all the values assigned to a set of variables
from another frontier. The values are of the type \lstinline{Ninfty}, and the function
only returns true if \textbf{all} the values from the first frontier are greater than
the values from the second frontier.

\begin{minipage}{\linewidth}
\begin{lstlisting}[language=Coq, label={lst:geq_def}, caption={Point-wise comparing frontiers with \lstinline{geq} in Coq}]
Fixpoint geq (V : set string) (g f : Frontier) : bool :=
  match V with
  | []      => true
  | h :: t  =>
    match g h with
    | infty => geq t g f
    | fin n =>
        match f h with
        | infty => false
        | fin k => (k <=? n) && geq t g f
        end
    end
  end.
\end{lstlisting}
\end{minipage}

\subsection{The predicate \lstinline{ex_lfp_geq} with \lstinline{Prop} and \lstinline{Set}}
\label{ssec:ex_lfp_geq}

We can now combine \lstinline{sub_model} and \lstinline{geq} to construct a predicate
stating that there exists a frontier \lstinline{g} that is a model of the set of clauses \lstinline{Cs}
and is greater than or equal to another frontier \lstinline{f}.

\begin{minipage}{\linewidth}
\begin{lstlisting}[language=Coq, label={lst:ex_lfp_geq_def}, caption={\lstinline{ex_lfp_geq} in Coq, using both \lstinline{Prop} and \lstinline{Set}}]
Definition ex_lfp_geq_P (Cs : set Clause) (V W : set string) (f : Frontier) : Prop :=
  exists g : Frontier, geq V g f = true /\ sub_model Cs V W g = true.

Definition ex_lfp_geq_S (Cs : set Clause) (V W : set string) (f : Frontier) : Set :=
  sig (fun g : Frontier => prod (geq V g f = true) (sub_model Cs V W g = true)).
\end{lstlisting}
\end{minipage}

One thing to note here is that we can define this predicate either with \lstinline{Prop} or with \lstinline{Set}.
When defined with \lstinline{Prop}, we define it as a logical predicate, using standard first-order logic syntax.
When defined with \lstinline{Set}, we define it as a type,
using the \lstinline{sig} type constructor in place of \lstinline{exists}.
\lstinline{sig} denotes the $\Sigma$-type, which we explained in \Cref{ssec:dependent_types}.

The reason for defining \lstinline{ex_lfp_geq} with \lstinline{Set}, is that we then can use
Coq's extraction feature to generate Haskell code from the Coq definitions.
As briefly mentioned in \Cref{ssec:propositions_as_types}, a proof can often be used to compute something.
In this case, we want to compute the actual frontier \lstinline{g} that satisfies the above predicate.
Coq distinguishes between logical objects (objects in \lstinline{Prop}) and informative objects (objects in \lstinline{Set})
\cite[p.~1-2]{coqextrnew}.
When extracting, Coq will remove as many logical objects as possible, meaning a proposition defined in \lstinline{Prop}
would simply be collapsed to "\lstinline[language=Haskell]{()}" (the unit type) when extracted to Haskell
\cite[p.~8]{coqextroverview}.
Logical objects are only used to ensure correctness when constructing a proof in Coq,
and are not needed when actually computing something using the extracted code.
Informative objects, on the other hand, are kept when extracting, and can be used to compute something.
This is why we define \lstinline{ex_lfp_geq} with \lstinline{Set}.

An important thing to we need to keep in mind is to avoid \emph{universe inconsistencies}.
These can occur by unwise use of \lstinline{Type}.
We will not elaborate much on what type universes are, but we will give a brief explanation of how they work in Coq.
In Coq, all objects of type \lstinline{Prop} also live in the universe \lstinline{Set}, and all objects of type \lstinline{Set}
also live in the universe \lstinline{Type}.
Moreover, \lstinline{Prop : Set} and \lstinline{Set : Type}.
The cumulative universe hierarchy extends infinitely past \lstinline{Type}, but we will not
need to go further than that.
When defining a type, we can only refer to objects in universes lower than the universe of the type we are defining.
Here is an example to illustrate this:

\begin{minipage}{\linewidth}
\begin{lstlisting}[language=Coq, label={lst:universe_inconsistency}, caption={A universe inconsistency in Coq}]

(* type signature of sig *)
sig : forall A : Type, (A -> Prop) -> Type

(* Set <= Set, OK *)
Definition valid_def (A : Set) : Set :=
  sig (fun x : A => x = x).

(* Set > Prop, universe inconsistency! *)
Fail Definition invalid_def (A : Set) : Prop :=
  sig (fun x : A => x = x).
\end{lstlisting}
\end{minipage}


\section{The main proofs}

We have now laid the groundwork for the formalization of \Cref{thm:3.2}.
We precede the definition of \Cref{thm:3.2} with two additional definitions, which helps us simplify its
definition and the proof of the theorem itself.

\subsection{The predicate \lstinline{pre_thm}}

Since our formal definitions of \Cref{lem:3.3}, which will be expanded on shortly,
and \Cref{thm:3.2} share some structure, we define (with \lstinline{Set}) a predicate \lstinline{pre_thm}:

\begin{minipage}{\linewidth}
\begin{lstlisting}[language=Coq, label={lst:pre_thm_def}, caption={\lstinline{pre_thm} in Coq}]
Definition pre_thm (n m : nat) (Cs : set Clause) (V W : set string) (f : Frontier)
    : Set :=
  incl W V ->
  Datatypes.length (nodup string_dec V) <= n ->
  Datatypes.length
    (set_diff string_dec
      (nodup string_dec V)
      (nodup string_dec W)
    ) <= m <= n ->
  ex_lfp_geq Cs (nodup string_dec W) (nodup string_dec W) f ->
  ex_lfp_geq Cs (nodup string_dec V) (nodup string_dec V) f.
\end{lstlisting}
\end{minipage}

\subsection{\Cref{lem:3.3}}

\Cref{lem:3.3} is used in the proof of \Cref{thm:3.2} to solve
a special case, as we saw in \Cref{sec:relevant_parts_of_the_paper}.
We also need this lemma to be able to prove \Cref{thm:3.2} in Coq.
As explained in \Cref{ssec:leaving_out_the_formal_proof_of_lem_33},
we will only give a formal definition of the lemma in Coq,
leave out the proof. By using the Coq command \lstinline{Admitted},
we can leave out a full proof of a lemma, but still be able to use the lemma in other Coq proofs.
We define \Cref{lem:3.3} in Coq using \lstinline{pre_thm}, as follows:

\begin{minipage}{\linewidth}
\begin{lstlisting}[language=Coq, label={lst:lem_33}, caption={\Cref{lem:3.3} in Coq}]
Lemma lem_33 :
  forall Cs : set Clause,
  forall V W : set string,
  forall f : Frontier,
    (forall Cs' : set Clause,
     forall V' W' : set string,
     forall f' : Frontier,
     forall m : nat,
      pre_thm (Datatypes.length (nodup string_dec V) - 1) m Cs' V' W' f'
    ) ->
    incl W V ->
    ex_lfp_geq Cs (nodup string_dec W) (nodup string_dec W) f ->
    ex_lfp_geq Cs (nodup string_dec V) (nodup string_dec W) f.
Admitted.
\end{lstlisting}
\end{minipage}

One thing to note is that when applying \Cref{lem:3.3} in the formal proof of \Cref{thm:3.2},
the first assumption of the lemma is solved immediately by applying the primary induction hypothesis
(the induction on \lstinline{n}, also called \lstinline{IHn} in Coq).

\subsection{\Cref{thm:3.2}}

We can now formulate \Cref{thm:3.2} using \lstinline{pre_thm}:

\begin{minipage}{\linewidth}
\begin{lstlisting}[language=Coq, label={lst:thm_32}, caption={\Cref{thm:3.2} in Coq}]
Theorem thm_32 :
  forall n m : nat,
  forall Cs : set Clause,
  forall V W : set string,
  forall f : Frontier,
    pre_thm n m Cs V W f.
Proof.
  (* ... *)
Qed.
\end{lstlisting}
\end{minipage}

The proof of \Cref{thm:3.2} is by primary induction on \lstinline{n} and secondary induction on \lstinline{m}.

\subsubsection{Base case of \lstinline{n}}

The first base case is simple. We directly prove the conclusion of \lstinline{pre_thm}:

(1) \tabto{2em}
\lstinline{ex_lfp_geq Cs (nodup string_dec V) (nodup string_dec V) f}.

Since \lstinline{n = 0}, we get that the length of \lstinline{V} is \lstinline{0},
and hence we get a new goal \lstinline{ex_lfp_geq Cs [] [] f}.
This is proven by the lemma \lstinline{ex_lfp_geq_empty},
which states that \lstinline{forall Cs f, ex_lfp_geq Cs [] [] f}.

\subsubsection{Inductive case of \lstinline{n}}

We start the inductive case of \lstinline{n} by doing a new induction on \lstinline{m}.

\subsubsection{Base case of \lstinline{m}}

The second base case is similar to the first base case of \lstinline{n}.
We again want to prove

\tabto{2em}
\lstinline{ex_lfp_geq Cs (nodup string_dec V) (nodup string_dec V) f}.

But now we have as assumption this same statement with \lstinline{W},
and \lstinline{incl V W} since \lstinline{m = 0}.
We now apply the lemma \lstinline{ex_lfp_geq_incl}, which states that

\tabto{2em}
\lstinline{forall Cs V W f, incl V W -> forall f, ex_lfp_geq Cs W W f -> ex_lfp_geq Cs V V f.}

We give this lemma the arguments of \lstinline{Cs}, \lstinline{nodup string_dec V} and \lstinline{nodup string_dec W}.
This generates two new goals:

$(1)$
\tabto{2em}
\lstinline{incl (nodup string_dec V) (nodup string_dec W)}

$(2)$
\tabto{2em}
\lstinline{ex_lfp_geq Cs (nodup string_dec W) (nodup string_dec W) f}

The goal $(1)$ is proven by using a hypothesis that states that

\tabto{2em}
\lstinline{Datatypes.length (set_diff string_dec (nodup string_dec V) (nodup string_dec W)) <= m <= n}.

Since \lstinline{m = 0}, this means that
the set difference of \lstinline{V} and \lstinline{W} is empty.
We can now apply the lemma \lstinline{set_diff_nil_incl} on this hypothesis, which states that

\tabto{2em}
\lstinline{forall dec V W, set_diff dec V W = [] <-> incl V W.}

This gives us a hypothesis identical to our goal $(1)$, and therefore proves it.

The goal $(2)$ is proven by an existing hypothesis.

\subsubsection{Inductive case of \lstinline{m}}
\label{sssec:inductive_case_m}

The inductive case of \lstinline{m} is by far the largest part of the proof (see lines 17-199 of \Cref{lst:thm_32_full_proof}).
As it is very complex and does not follow the informal proof in \cite{mbezem} too closely,
we will go through a more "high-level" explanation of the reasoning
we followed when constructing the formalized proof.
We will sometimes use mathematical notation to explain the proof instead of Coq code,
for the sake of being terse and clear.

Assume $W \subseteq V$, $\lvert V \rvert \leq n + 1$, $\lvert V - W \rvert \leq m + 1 \leq n + 1$.
If $\lvert V - W \rvert \leq m$ then we can apply the secondary induction hypothesis, and we are done.
Hence, we can assume $\lvert V - W \rvert = m + 1$, implying $\lvert W \rvert \leq n$.
To prove \lstinline{ex_lfp_geq Cs V V f}, we assert \lstinline{(W, g) = sub_forward Cs V V f}.
This gives us three cases to consider:

\begin{enumerate}
  \item $W = \emptyset$, and we are done.
  \item $W = V$, meaning all variables in $V$ map to $\infty$, and we are done.
  \item $\emptyset \subset W \subset V$. Then $\lvert W \rvert \leq n$ and $\lvert V - W \rvert \leq m$.
      So by the primary induction hypothesis, we get \lstinline{ex_lfp_geq Cs W W f}
      (line 31 of \Cref{lst:thm_32_full_proof}).
      Then by the second induction hypothesis and the application of \Cref{lem:3.3} (line 40 of \Cref{lst:thm_32_full_proof}),
      we get \lstinline{ex_lfp_geq Cs V V f}, and we are done.
\end{enumerate}

A complete formal proof of \Cref{thm:3.2} can be found in \Cref{lst:thm_32_full_proof}.

\section{Extraction to Haskell}

Using Coq's code extraction feature, we can extract Haskell code from our Coq definitions.

\begin{minipage}{\linewidth}
\begin{lstlisting}[language=Coq, label={lst:extraction}, caption={Extraction of Coq definitions to Haskell}]
Extraction Language Haskell.

Extract Constant map => "Prelude.map".
Extract Constant fold_right => "Prelude.foldr".

Extraction "/home/user/path/to/code/ex.hs"
  thm_32
  lem_33.
\end{lstlisting}
\end{minipage}

Coq will automatically determine definitions which depend on one another when doing extraction.
In the example above, we would not have needed to specify \lstinline{lem_33} to be extracted,
since \lstinline{thm_32} already depends on it.

By default, Coq will give its own implementation of any functions used, instead of using
Haskell's native implementations. If we want, we can specify what native Haskell functions
should be used when extracting a Coq function. In the example code above,
we specify that when extracting, Haskell's \lstinline[language=Haskell]{Prelude.map} and \lstinline[language=Haskell]{Prelude.foldr}
should be used for the Coq functions \lstinline{map} and \lstinline{fold_right}.

In the next chapter we will go more into detail about the results of the extraction,
and the results of the Haskell code ran on some example input.
